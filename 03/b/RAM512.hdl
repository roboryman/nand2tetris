// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * 512-Register RAM:
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 *
 * PINS:
 *  + IN: in[16], load, address[9]
 *  + OUT: out[16]
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    // MSB address: [(k=3)..(k-1)] = [6..8]
    // Lower RAM chips address: [0..(k-4)] = [0..5]

    PARTS:
    DMux8Way(in=load, sel=address[6..8], a=loadRAM0, b=loadRAM1, c=loadRAM2, d=loadRAM3, e=loadRAM4, f=loadRAM5, g=loadRAM6, h=loadRAM7);
    RAM64(in=in, load=loadRAM0, address=address[0..5], out=RAM0Out);
    RAM64(in=in, load=loadRAM1, address=address[0..5], out=RAM1Out);
    RAM64(in=in, load=loadRAM2, address=address[0..5], out=RAM2Out);
    RAM64(in=in, load=loadRAM3, address=address[0..5], out=RAM3Out);
    RAM64(in=in, load=loadRAM4, address=address[0..5], out=RAM4Out);
    RAM64(in=in, load=loadRAM5, address=address[0..5], out=RAM5Out);
    RAM64(in=in, load=loadRAM6, address=address[0..5], out=RAM6Out);
    RAM64(in=in, load=loadRAM7, address=address[0..5], out=RAM7Out);
    Mux8Way16(a=RAM0Out, b=RAM1Out, c=RAM2Out, d=RAM3Out, e=RAM4Out, f=RAM5Out, g=RAM6Out, h=RAM7Out, sel=address[6..8], out=out);
}
